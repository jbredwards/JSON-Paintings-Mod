buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = 'v1.3.0-mc1.12.2'
group = 'git.jbredwards.jsonpaintings' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'JSON-Paintings'
sourceCompatibility  = targetCompatibility = '1.8'

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'
    mappings = 'stable_39'
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven { url 'https://dvs1.progwml6.com/files/maven' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.covers1624.net' }
    maven { url 'https://maven.tterrag.com' }
}

dependencies {
    deobfCompile 'cofh:RedstoneFlux:1.12-2.0.1.2:universal' // for TOP
    deobfCompile 'com.github.TehNut-Mods:HWYLA:1.12-SNAPSHOT'
    deobfCompile 'mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.19-11'
    deobfCompile 'mezz.jei:jei_1.12.2:4.15.0.276'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourcesJar
        }
    }
}

//for applying asm outside dev environment
jar { 
    manifest { 
        attributes([
            'FMLCorePlugin': 'git.jbredwards.jsonpaintings.mod.asm.ASMHandler',
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLAT': 'jsonpaintings_at.cfg'
        ])
    }
}

//for applying asm with intellij
runClient { jvmArgs '-Dfml.coreMods.load=git.jbredwards.jsonpaintings.mod.asm.ASMHandler' }
runServer { jvmArgs '-Dfml.coreMods.load=git.jbredwards.jsonpaintings.mod.asm.ASMHandler' }